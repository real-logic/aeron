/*
 * Copyright 2014-2020 Real Logic Limited.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'idea'
    id 'io.freefair.javadoc-links' version '4.1.6' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
    id 'biz.aQute.bnd.builder' version '5.0.0' apply false
    id "com.github.ben-manes.versions" version "0.27.0"
}

defaultTasks 'clean', 'build', 'shadowJar', 'install'

def aeronGroup = 'io.aeron'
def aeronVersion = file('version.txt').text.trim()

def agronaVersion = '1.3.0'
def sbeVersion = '1.16.1'
def checkstyleVersion = '8.28'
def hamcrestVersion = '2.2'
def junitVersion = '5.6.0'
def mockitoVersion = '3.2.4'
def byteBuddyVersion = '1.10.5'
def hdrHistogramVersion = '2.1.12'

group = aeronGroup
version = aeronVersion
ext.isReleasedVersion = !version.endsWith("SNAPSHOT")

ext {
    group = aeronGroup
    version = aeronVersion

    if (!project.hasProperty('ossrhUsername')) {
        ossrhUsername = ''
    }

    if (!project.hasProperty('ossrhPassword')) {
        ossrhPassword = ''
    }

    if (!project.hasProperty('releaseUrl')) {
        releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    }

    if (!project.hasProperty('snapshotUrl')) {
        snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

def projectPom = {
    name = 'aeron'
    packaging = 'pom'
    // optionally artifactId can be defined here
    description = 'Efficient reliable UDP unicast, UDP multicast, and IPC transport protocol.'
    url = 'https://github.com/real-logic/aeron'

    scm {
        connection = 'scm:git:https://github.com/real-logic/aeron.git'
        developerConnection = 'scm:git:https://github.com/real-logic/aeron.git'
        url = 'https://github.com/real-logic/aeron.git'
    }

    licenses {
        license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id = 'tmontgomery'
            name = 'Todd L. Montgomery'
            email = 'tmont@nard.net'
            url = 'https://github.com/tmontgomery'
        }
        developer {
            id = 'mjpt777'
            name = 'Martin Thompson'
            email = 'mjpt777@gmail.com'
            url = 'https://github.com/mjpt777'
        }
        developer {
            id = 'RichardWarburton'
            name = 'Richard Warburton'
            email = 'richard.warburton@gmail.com'
            url = 'https://github.com/RichardWarburton'
        }
        developer {
            id = 'nitsanw'
            name = 'Nitsan Wakart'
            email = 'nitsanw@yahoo.com'
            url = 'https://github.com/nitsanw'
        }
        developer {
            id = 'mikeb01'
            name = 'Mike Barker'
            email = 'mikeb01@gmail.com'
            url = 'https://github.com/mikeb01'
        }
        developer {
            id = 'vyazelenko'
            name = 'Dmitry Vyazelenko'
            email = 'vyazelenko@yahoo.com'
            url = 'https://github.com/vyazelenko'
        }
    }
}

configurations {
    shadow
}

jar.enabled = false

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'
    apply plugin: 'signing'
    apply plugin: 'eclipse'
    apply plugin: 'io.freefair.javadoc-links'

    group = aeronGroup
    version = aeronVersion

    jar.enabled = true
    checkstyle.toolVersion = "${checkstyleVersion}"

    dependencies {
        testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }

    compileJava {
        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.net=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        }
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    compileTestJava {
        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.net=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        }
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    test {
        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
            jvmArgs('--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/java.net=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/java.util.zip=ALL-UNNAMED')
            jvmArgs('--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED')
        }

        useJUnitPlatform {
            excludeTags 'slow'
        }

        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }

        systemProperties(
            'aeron.shared.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.conductor.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.sender.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.receiver.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.use.windows.high.res.timer': 'true',
            'aeron.timer.interval': '10000000',
            'aeron.dir.delete.on.start': 'true',
            'aeron.dir.delete.on.shutdown': 'true',
            'aeron.term.buffer.sparse.file': 'true',
            'aeron.perform.storage.checks': 'false')

        reports.html.enabled = false // Disable individual test reports
    }

    task slowTest(type: Test) {
        maxParallelForks = 1

        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
            jvmArgs('--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/java.net=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')
            jvmArgs('--add-opens', 'java.base/java.util.zip=ALL-UNNAMED')
            jvmArgs('--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED')
        }

        useJUnitPlatform {
            includeTags 'slow'
        }

        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }

        systemProperties(
            'aeron.shared.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.conductor.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.sender.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.receiver.idle.strategy': 'org.agrona.concurrent.YieldingIdleStrategy',
            'aeron.use.windows.high.res.timer': 'true',
            'aeron.timer.interval': '10000000',
            'aeron.perform.storage.checks': 'false'
        )

        reports.html.enabled = false // Disable individual test reports
    }

    javadoc {
        title = '<h1>Aeron Message Transport</h1>'
        options.bottom = '<i>Copyright &#169; 2014-2020 Real Logic Limited. All Rights Reserved.</i>'
        options.encoding = 'UTF-8'
        options.docEncoding = 'UTF-8'
        options.charSet = 'UTF-8'
        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_10.ordinal()) {
            options.addBooleanOption 'html5', true
        }
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        archiveClassifier.set "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    artifacts {
        tests testJar
    }

    signing {
        required { isReleasedVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    install {
        repositories.mavenInstaller.pom.project(projectPom)
        repositories.mavenInstaller.pom.whenConfigured {
            p -> p.dependencies = p.dependencies.findAll {
                dep -> !(dep.artifactId.contains("hamcrest") ||
                         dep.artifactId.contains("mockito") ||
                         dep.artifactId.contains("junit") ||
                         dep.artifactId.contains("aeron-test-support"))
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.whenConfigured {
                    p -> p.dependencies = p.dependencies.findAll {
                        dep -> !(dep.artifactId.contains("hamcrest") ||
                                 dep.artifactId.contains("mockito") ||
                                 dep.artifactId.contains("junit") ||
                                 dep.artifactId.contains("aeron-test-support"))
                        }
                }
            }
        }
    }
}

project(':aeron-client') {
    apply plugin: 'biz.aQute.bnd.builder'

    dependencies {
        api "org.agrona:agrona:${agronaVersion}"
        testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    jar {
        bnd """
            Automatic-Module-Name:  io.aeron.client
            Bundle-Name:            io.aeron.client
            Bundle-SymbolicName:    io.aeron.client
            Implementation-Title:   Aeron
            Implementation-Vendor:  Real Logic Limited
            Implementation-Version: ${aeronVersion}

            -exportcontents: io.aeron, io.aeron.*

            # Suppress headers that reduce reproducibility.
            -reproducible: true
            -noextraheaders: true
        """
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':aeron-driver') {
    apply plugin: 'application'
    apply plugin: 'biz.aQute.bnd.builder'

    mainClassName = 'io.aeron.driver.MediaDriver'

    dependencies {
        api project(':aeron-client')
        testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    jar {
        bnd """
            Automatic-Module-Name:  io.aeron.driver
            Bundle-Name:            io.aeron.driver
            Bundle-SymbolicName:    io.aeron.driver
            Implementation-Title:   Aeron
            Implementation-Vendor:  Real Logic Limited
            Implementation-Version: ${aeronVersion}

            -exportcontents: io.aeron.driver, io.aeron.driver.*

            # Suppress headers that reduce reproducibility.
            -reproducible: true
            -noextraheaders: true
        """
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    build.doLast {
        distZip
    }
}

project(':aeron-archive') {
    apply plugin: 'biz.aQute.bnd.builder'

    configurations{
        codecGeneration
    }

    dependencies {
        api project(':aeron-driver')
        api files('build/classes/java/generated')
        codecGeneration "uk.co.real-logic:sbe-tool:${sbeVersion}"
        testImplementation project(':aeron-test-support')
    }

    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    compileJava.dependsOn 'compileGeneratedJava'

    compileGeneratedJava {
        dependsOn 'generateCodecs'
        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        }
        options.encoding = 'UTF-8'
        options.deprecation = true
        classpath += configurations.codecGeneration
    }

    task generateCodecs(type: JavaExec) {
        def codecsFile = 'src/main/resources/archive/aeron-archive-codecs.xml'
        def markCodecsFile = 'src/main/resources/archive/aeron-archive-mark-codecs.xml'
        def sbeFile = 'src/main/resources/archive/fpl/sbe.xsd'

        inputs.files(codecsFile, markCodecsFile, sbeFile)
        outputs.dir generatedDir

        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = configurations.codecGeneration
        systemProperties(
            'sbe.output.dir': generatedDir,
            'sbe.target.language': 'Java',
            'sbe.validation.xsd': sbeFile,
            'sbe.validation.stop.on.error': 'true')
        args = [codecsFile, markCodecsFile]
    }

    def generatedCppDir = file(System.properties['codec.target.dir'] ?: "${rootDir}/cppbuild/Release/generated")
    task generateCppCodecs(type: JavaExec) {
        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = configurations.codecGeneration
        systemProperties(
            'sbe.output.dir': generatedCppDir,
            'sbe.target.language': 'Cpp',
            'sbe.target.namespace': 'aeron.archive.client',
            'sbe.validation.xsd': 'src/main/resources/archive/fpl/sbe.xsd',
            'sbe.validation.stop.on.error': 'true')
        args = ['src/main/resources/archive/aeron-archive-codecs.xml']
    }

    jar {
        from("${buildDir}/classes/java/generated") {
            include '**/*.class'
        }

        bnd """
            Automatic-Module-Name:  io.aeron.archive
            Bundle-Name:            io.aeron.archive
            Bundle-SymbolicName:    io.aeron.archive
            Implementation-Title:   Aeron
            Implementation-Vendor:  Real Logic Limited
            Implementation-Version: ${aeronVersion}

            -exportcontents: io.aeron.archive, io.aeron.archive.*

            # Suppress headers that reduce reproducibility.
            -reproducible: true
            -noextraheaders: true
        """
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    task sourcesJar(type: Jar) {
        dependsOn generateCodecs
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
        from sourceSets.generated.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':aeron-cluster') {
    apply plugin: 'biz.aQute.bnd.builder'

    configurations{
        codecGeneration
    }

    dependencies {
        api project(':aeron-archive')
        api files('build/classes/java/generated')
        codecGeneration "uk.co.real-logic:sbe-tool:${sbeVersion}"
        testImplementation project(':aeron-test-support')
    }

    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    compileJava.dependsOn 'compileGeneratedJava'

    compileGeneratedJava {
        dependsOn 'generateCodecs'
        if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_1_9.ordinal()) {
            options.compilerArgs.addAll(['--add-exports', 'java.base/java.lang.reflect=ALL-UNNAMED'])
            options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
        }
        options.encoding = 'UTF-8'
        options.deprecation = true
        classpath += configurations.codecGeneration
    }

    task generateCodecs(type: JavaExec) {
        def codecsFile = 'src/main/resources/cluster/aeron-cluster-codecs.xml'
        def markCodecsFile = 'src/main/resources/cluster/aeron-cluster-mark-codecs.xml'
        def sbeFile = 'src/main/resources/cluster/fpl/sbe.xsd'

        inputs.files(codecsFile, markCodecsFile, sbeFile)
        outputs.dir generatedDir

        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = configurations.codecGeneration
        systemProperties(
            'sbe.output.dir': generatedDir,
            'sbe.target.language': 'Java',
            'sbe.validation.xsd': sbeFile,
            'sbe.validation.stop.on.error': 'true')
        args = [codecsFile, markCodecsFile]
    }

    jar {
        from("${buildDir}/classes/java/generated") {
            include '**/*.class'
        }

        bnd """
            Automatic-Module-Name:  io.aeron.cluster
            Bundle-Name:            io.aeron.cluster
            Bundle-SymbolicName:    io.aeron.cluster
            Implementation-Title:   Aeron
            Implementation-Vendor:  Real Logic Limited
            Implementation-Version: ${aeronVersion}

            -exportcontents: io.aeron.cluster, io.aeron.cluster.*

            # Suppress headers that reduce reproducibility.
            -reproducible: true
            -noextraheaders: true
        """
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    task sourcesJar(type: Jar) {
        dependsOn generateCodecs
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
        from sourceSets.generated.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':aeron-agent') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        implementation project(':aeron-cluster')
        implementation "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
        testImplementation project(':aeron-test-support')
    }

    jar {
        manifest.attributes(
            'Implementation-Title': 'Aeron',
            'Implementation-Version': "${aeronVersion}",
            'Implementation-Vendor': 'Real Logic Limited',
            "Premain-Class": "io.aeron.agent.EventLogAgent",
            "Agent-Class": "io.aeron.agent.EventLogAgent",
            "Can-Redefine-Classes": "true",
            "Can-Retransform-Classes": "true",
            'Automatic-Module-Name': 'io.aeron.agent'
        )
    }

    shadowJar {
        exclude 'io/aeron/archive'
        exclude 'io/aeron/cluster'
        dependencies {
            exclude(dependency('org.agrona:agrona'))
            exclude(project(':aeron-client'))
            exclude(project(':aeron-driver'))
            exclude(project(':aeron-archive'))
            exclude(project(':aeron-cluster'))
        }
        manifest.attributes(
            'Implementation-Title': 'Aeron',
            'Implementation-Version': "${aeronVersion}",
            'Implementation-Vendor': 'Real Logic Limited',
            "Premain-Class": "io.aeron.agent.EventLogAgent",
            "Agent-Class": "io.aeron.agent.EventLogAgent",
            "Can-Redefine-Classes": "true",
            "Can-Retransform-Classes": "true",
            'Automatic-Module-Name': 'io.aeron.agent.all'
        )
    }

    jar.finalizedBy shadowJar

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.whenConfigured {
                    p -> p.dependencies = []
                }

                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    uploadShadow {
        repositories {
            mavenDeployer {
                pom.whenConfigured {
                    p -> p.dependencies = []
                }

                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }

        mustRunAfter 'uploadArchives'
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    install {
        repositories.mavenInstaller.pom.whenConfigured {
            p -> p.dependencies = []
        }
    }

    signing {
        required { isReleasedVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.shadow
    }
}

project(':aeron-samples') {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        api project(':aeron-archive')
        implementation "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    signing {
        required { isReleasedVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.shadow
    }
}

project(':aeron-system-tests') {
    dependencies {
        testImplementation project(':aeron-archive')
        testImplementation project(':aeron-test-support')
        testImplementation project(path: ':aeron-client', configuration: 'tests')
        testImplementation project(path: ':aeron-archive', configuration: 'tests')
    }

    test {
        systemProperties('java.net.preferIPv4Stack': 'true')
        systemProperties('aeron.test.system.aeronmd.path': System.getProperty('aeron.test.system.aeronmd.path'))
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from sourceSets.test.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

project(':aeron-test-support') {
    dependencies {
        api "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        api "org.agrona:agrona:${agronaVersion}"
    }
}

project(':aeron-all') {
    apply plugin: 'com.github.johnrengelman.shadow'

    def aeronClientProject = project(':aeron-client')
    def aeronDriverProject = project(':aeron-driver')
    def aeronArchiveProject = project(':aeron-archive')
    def aeronClusterProject = project(':aeron-cluster')
    def aeronSamplesProject = project(':aeron-samples')

    dependencies {
        implementation aeronClusterProject
        implementation aeronSamplesProject
        implementation "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
    }

    javadoc {
        source += aeronClientProject.sourceSets.main.allJava
        source += aeronDriverProject.sourceSets.main.allJava
        source += aeronArchiveProject.sourceSets.main.allJava
        source += aeronArchiveProject.sourceSets.generated.allJava
        source += aeronClusterProject.sourceSets.main.allJava
        source += aeronClusterProject.sourceSets.generated.allJava
        source += aeronSamplesProject.sourceSets.main.allJava
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set 'sources'
        from files(
            aeronClientProject.sourceSets.main.allSource,
            aeronDriverProject.sourceSets.main.allSource,
            aeronArchiveProject.sourceSets.main.allSource,
            aeronArchiveProject.sourceSets.generated.allSource,
            aeronClusterProject.sourceSets.main.allSource,
            aeronClusterProject.sourceSets.generated.allSource,
            aeronSamplesProject.sourceSets.main.allSource)
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set 'javadoc'
        from javadoc.destinationDir
    }

    shadowJar {
        archiveClassifier.set ''
        manifest.attributes(
            'Implementation-Title': 'Aeron',
            'Implementation-Version': "${aeronVersion}",
            'Implementation-Vendor': 'Real Logic Limited',
            'Automatic-Module-Name': 'io.aeron.all')
        relocate 'org.HdrHistogram', 'io.aeron.shadow.org.HdrHistogram'
    }

    jar.finalizedBy shadowJar

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                pom.whenConfigured {
                    p -> p.dependencies = []
                }

                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }
    }

    uploadShadow {
        repositories {
            mavenDeployer {
                pom.whenConfigured {
                    p -> p.dependencies = []
                }

                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: releaseUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotUrl) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project(projectPom)
            }
        }

        mustRunAfter 'uploadArchives'
    }

    install {
        repositories.mavenInstaller.pom.whenConfigured {
            p -> p.dependencies = []
        }
    }

    signing {
        required { isReleasedVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.shadow
    }
}

task uploadToMavenCentral {
    dependsOn 'aeron-client:uploadArchives',
        'aeron-driver:uploadArchives',
        'aeron-samples:uploadArchives',
        'aeron-archive:uploadArchives',
        'aeron-all:uploadArchives',
        'aeron-all:uploadShadow',
        'aeron-agent:uploadArchives',
        'aeron-agent:uploadShadow',
        'aeron-cluster:uploadArchives'
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

wrapper {
    gradleVersion = '6.1.1'
    distributionType = 'ALL'
}
